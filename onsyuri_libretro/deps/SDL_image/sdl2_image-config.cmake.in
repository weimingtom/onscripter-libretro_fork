# sdl2_image cmake project-config input for ./configure scripts

include(FeatureSummary)
set_package_properties(SDL2_image PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_image/"
    DESCRIPTION "SDL_image is an image file loading library"
)

set(SDL2_image_FOUND TRUE)

set(SDL2IMAGE_AVIF  @SDL2IMAGE_AVIF_ENABLED@)
set(SDL2IMAGE_BMP   @SDL2IMAGE_BMP_ENABLED@)
set(SDL2IMAGE_GIF   @SDL2IMAGE_GIF_ENABLED@)
set(SDL2IMAGE_JPG   @SDL2IMAGE_JPG_ENABLED@)
set(SDL2IMAGE_JXL   @SDL2IMAGE_JXL_ENABLED@)
set(SDL2IMAGE_LBM   @SDL2IMAGE_LBM_ENABLED@)
set(SDL2IMAGE_PCX   @SDL2IMAGE_PCX_ENABLED@)
set(SDL2IMAGE_PNG   @SDL2IMAGE_PNG_ENABLED@)
set(SDL2IMAGE_PNM   @SDL2IMAGE_PNM_ENABLED@)
set(SDL2IMAGE_QOI   @SDL2IMAGE_QOI_ENABLED@)
set(SDL2IMAGE_SVG   @SDL2IMAGE_SVG_ENABLED@)
set(SDL2IMAGE_TGA   @SDL2IMAGE_TGA_ENABLED@)
set(SDL2IMAGE_TIF   @SDL2IMAGE_TIF_ENABLED@)
set(SDL2IMAGE_XCF   @SDL2IMAGE_XCF_ENABLED@)
set(SDL2IMAGE_XPM   @SDL2IMAGE_XPM_ENABLED@)
set(SDL2IMAGE_XV    @SDL2IMAGE_XV_ENABLED@)
set(SDL2IMAGE_WEBP  @SDL2IMAGE_WEBP_ENABLED@)

set(SDL2IMAGE_JPG_SAVE @SDL2IMAGE_JPG_SAVE@)
set(SDL2IMAGE_PNG_SAVE @SDL2IMAGE_PNG_SAVE@)

set(SDL2IMAGE_VENDORED  FALSE)

set(SDL2IMAGE_BACKEND_IMAGEIO   @SDL2IMAGE_IMAGEIO_ENABLED@)
set(SDL2IMAGE_BACKEND_STB       @SDL2IMAGE_STB_ENABLED@)
set(SDL2IMAGE_BACKEND_WIC       @SDL2IMAGE_WIC_ENABLED@)

get_filename_component(CMAKE_CURRENT_LIST_DIR ${CMAKE_CURRENT_LIST_DIR} REALPATH)
get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}/@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl2image_extra_static_libraries "@IMG_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl2image_extra_static_libraries}" _sdl2image_extra_static_libraries)

set(_sdl2image_bindir   "${bindir}")
set(_sdl2image_libdir   "${libdir}")
set(_sdl2image_incdir   "${includedir}/SDL2")

# Convert _sdl2image_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl2image_extra_static_libraries "${_sdl2image_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl2image_extra_static_libraries "${_sdl2image_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl2image_extra_static_libraries "${_sdl2image_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL2_image::SDL2_image)
    add_library(SDL2_image::SDL2_image SHARED IMPORTED)
    set_target_properties(SDL2_image::SDL2_image
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl2image_incdir}"
            COMPATIBLE_INTERFACE_BOOL "SDL2_SHARED"
            INTERFACE_SDL2_SHARED "ON"
    )
    if(WIN32)
        set_target_properties(SDL2_image::SDL2_image
            PROPERTIES
                IMPORTED_LOCATION "${_sdl2image_bindir}/SDL2_image.dll"
                IMPORTED_IMPLIB "${_sdl2image_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_image.dll${CMAKE_STATIC_LIBRARY_SUFFIX}"
        )
    else()
        set_target_properties(SDL2_image::SDL2_image
            PROPERTIES
                IMPORTED_LOCATION "${_sdl2image_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL2_image${CMAKE_SHARED_LIBRARY_SUFFIX}"
        )
    endif()
endif()

if(NOT TARGET SDL2_image::SDL2_image-static)
    add_library(SDL2_image::SDL2_image-static STATIC IMPORTED)

    set_target_properties(SDL2_image::SDL2_image-static
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl2image_incdir}"
            IMPORTED_LOCATION "${_sdl2image_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_image${CMAKE_STATIC_LIBRARY_SUFFIX}"
            INTERFACE_LINK_LIBRARIES "${_sdl2image_extra_static_libraries}"
    )
endif()

unset(_sdl2image_extra_static_libraries)
unset(_sdl2image_bindir)
unset(_sdl2image_libdir)
unset(_sdl2image_incdir)
